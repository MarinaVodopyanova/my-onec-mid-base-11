
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// {{vodopyanova: создание группы "согласованная скидка" на форме,добавление в нее команды и поля ввода для реквизита

	ГруппаФормы = Элементы.Вставить("ГруппаСогласованнаяСкидка",Тип("ГруппаФормы"),,Элементы.Страницы);
	ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Элементы.ГруппаСогласованнаяСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.neto_СогласованнаяСкидка";
	//@skip-check wrong-string-literal-content
	ПолеВвода.УстановитьДействие("ПриИзменении","СогласованнаяСкидкаПриИзменении");
 	КомандаФормы = Команды.Добавить("ПересчитатьТаблицу");
	КомандаФормы.Заголовок = "ПересчитатьТаблицу";
	КомандаФормы.Действие = "ПересчитатьТаблицуНажатие";
	
	Кнопка = Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"),Элементы.ГруппаСогласованнаяСкидка);
	Кнопка.ИмяКоманды = "ПересчитатьТаблицу";
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.Картинка = БиблиотекаКартинок.Обновить;
	
// vodopyanova}}
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// {{vodopyanova: при изменении скидки и наличии строк в табчасти перейти к вопросу о согласии клиента на пересчет сумм с учетом скидки

&НаКлиенте
Процедура СогласованнаяСкидкаПриИзменении(Элемент)
	
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.Услуги.Количество() > 0 Тогда
		ЗадатьВопросОПродолжении();
	КонецЕсли;
	
КонецПроцедуры

// vodopyanova}}

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// {{vodopyanova: обработчик команды пересчета суммы документа и сумм табчасти с учетом скидки

&НаКлиенте
Процедура ПересчитатьТаблицуНажатие()
	
	Скидка  = Объект.neto_СогласованнаяСкидка;
	
	Для каждого Стр Из Объект.Товары Цикл;
		СуммаБезСкидки = Стр.Количество * Стр.Цена;
		Стр.Сумма = СуммаБезСкидки - СуммаБезСкидки * Скидка/100;
	КонецЦикла;
	
	Для каждого Стр Из Объект.Услуги Цикл;
		СуммаБезСкидки = Стр.Количество * Стр.Цена;
		Стр.Сумма = СуммаБезСкидки - СуммаБезСкидки * Скидка/100;
	КонецЦикла;
	
	РассчитатьСуммуДокумента();
		
КонецПроцедуры

// vodopyanova}}

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

// {{vodopyanova:вопрос клиенту о согласии на пересчет сумм в строках табличной части и суммы документа с учетом скидки 

&НаКлиенте
Асинх Процедура ЗадатьВопросОПродолжении()
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ =  Ждать ВопросАсинх("Скидка изменилась, пересчитать таблицу?", Режим);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПересчитатьТаблицуНажатие();
	КонецЕсли;
	
КонецПроцедуры

// vodopyanova}}

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

